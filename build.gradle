buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
    configurations.all {
        resolutionStrategy {
            force 'org.ow2.asm:asm:6.0_BETA'
        }
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

group = 'com.intellectualcrafters'

def rootVersion = "4"
def revision = ""
def buildNumber = ""
def date = ""
ext {
    git = org.ajoberstar.grgit.Grgit.open(file(".git"))
    date = git.head().date.format("yy.MM.dd")
    revision = "-${git.head().abbreviatedId}"
    parents = git.head().parentIds;
    index = 0;  // Offset to match CI
    for (; parents != null && !parents.isEmpty(); index++) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
    }
    buildNumber = "-${index}"
}

version = rootVersion + date + revision + buildNumber

description = rootProject.name
if (project.hasProperty("lzNoVersion")) { // gradle build -PlzNoVersion
    version = "unknown";
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'com.github.johnrengelman.shadow'

    lombok {
        version = '1.18.2'
        sha256 = ""
    }

    dependencies {
        compile(group: 'com.sk89q.worldedit', name: 'worldedit-core', version: '6.1.3-SNAPSHOT') {
            exclude(module: 'bukkit-classloader-check')
            exclude(module: 'mockito-core')
            exclude(module: 'dummypermscompat')
        }
        compile 'com.google.guava:guava:10.0'
        compileOnly 'org.projectlombok:lombok:1.18.4'
    }
    clean {
        delete file("../target")
    }
    repositories {
        mavenCentral()
        maven { url "http://empcraft.com/maven2" }
        maven { url "http://maven.sk89q.com/repo/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}
