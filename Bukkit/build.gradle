plugins {
    id "com.github.johnrengelman.shadow"
}
repositories {
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://repo.codemc.org/repository/maven-public" }
    maven { url = "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven {
        name = "papermc"
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    mavenLocal()
}


dependencies {
    implementation(project(":Core"))
    compile("org.bstats:bstats-bukkit:1.7")
    compile(project(":Core"))
    compile("com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT")
    //implementation 'com.onarandombox.multiversecore:Multiverse-Core:3.0.0-SNAPSHOT'
    implementation("org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT")
    compile(group: "com.sk89q.worldedit", name: "worldedit-bukkit", version: "7.0.1")
    compile("io.papermc:paperlib:1.0.2")
    implementation("net.kyori:text-adapter-bukkit:3.0.3")
    compile("com.github.MilkBowl:VaultAPI:1.7") {
        exclude(module: "bukkit")
    }
    implementation("me.clip:placeholderapi:2.10.4")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from("src/main/resources") {
        include("plugin.yml")
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

//noinspection GroovyAssignabilityCheck
jar.archiveFileName = "PlotSquared-Bukkit-${project.parent.version}.jar"
jar.destinationDirectory = file("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-Bukkit/" + project.parent.version)
task createPom {
    doLast {
        pom {
            project {
                groupId = rootProject.group
                artifactId = "PlotSquared-Bukkit"
                version = rootProject.version
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-Bukkit/${project.parent.version}/PlotSquared-Bukkit-${project.parent.version}.pom")
        pom {
            project {
                groupId = rootProject.group
                artifactId = "PlotSquared-Bukkit"
                version = "latest"
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-Bukkit/latest/PlotSquared-Bukkit-latest.pom")
         .writeTo("pom.xml")
    }
}

task copyFiles {
    doLast {
        copy {
            from("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-Bukkit/${project.parent.version}/")
            into("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-Bukkit/latest/")
            include("PlotSquared-Bukkit*.jar")
            rename("PlotSquared-Bukkit-${project.parent.version}.jar", "PlotSquared-Bukkit-latest.jar")
        }
    }
}


shadowJar {
    dependencies {
        include(dependency(":Core"))
        include(dependency("io.papermc:paperlib:1.0.2"))
        include(dependency("net.kyori:text-adapter-bukkit:3.0.3"))
        include(dependency("org.bstats:bstats-bukkit:1.7"))
    }
    relocate('net.kyori.text', 'com.github.intellectualsites.plotsquared.formatting.text')
    relocate("io.papermc.lib", "com.github.intellectualsites.plotsquared.bukkit.paperlib")
    relocate("org.bstats", "com.github.intellectualsites.plotsquared.metrics")
    archiveFileName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDirectory = file "../target"
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar)
build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
