repositories {
    maven {url "https://hub.spigotmc.org/nexus/content/groups/public/"}
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/"}
    maven {url "http://nexus.hc.to/content/repositories/pub_releases"}
    maven { url = "https://repo.codemc.org/repository/maven-public" }
    mavenLocal()
}

dependencies {
    compile project(':Core')
    compile 'org.spigotmc:spigot-api:1.12.1-R0.1-SNAPSHOT'
    compile(group: 'org.bstats', name: 'bstats-bukkit', version: '1.4')
    compile("net.milkbowl.vault:VaultAPI:1.6") {
        exclude module: 'bukkit'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(
            name: project.parent.name,
            version: project.parent.version
        )
    }
}
// We only want the shadow jar produced
jar.enabled = false

shadowJar {
    dependencies {
        include(dependency(':Core'))
        include(dependency('org.bstats:bstats-bukkit:1.4'))
    }
    relocate('org.mcstats', 'com.plotsquared.stats')
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
}

shadowJar.doLast {
    task ->
    ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar);
