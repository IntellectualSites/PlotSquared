repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    mavenLocal()
}

dependencies {
    compile project(':Core')
    compile 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
    compile(group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '7.0.0-SNAPSHOT')
    compile("net.milkbowl.vault:VaultAPI:1.6") {
        exclude module: 'bukkit'
    }
    compileOnly 'org.projectlombok:lombok:1.16.18'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}
// We only want the shadow jar produced
jar.enabled = false

shadowJar {
    dependencies {
        include(dependency(':Core'))
    }
    relocate('org.mcstats', 'com.plotsquared.stats')
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
}

shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar);
