repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "org.yaml:snakeyaml:1.23"
    //keep inline with Minecraft which uses gson 2.8.0
    implementation "com.google.code.gson:gson:2.8.0"
    implementation("org.projectlombok:lombok:1.18.6")
    compileOnly("org.projectlombok:lombok:1.18.6")
    testCompileOnly("org.projectlombok:lombok:1.18.6")
    annotationProcessor("org.projectlombok:lombok:1.18.6")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.6")
    implementation "net.kyori:text-adapter-bukkit:3.0.2"
    implementation "com.github.Sauilitired:Jenkins4J:2.0-SNAPSHOT"
    implementation "com.squareup.okhttp3:okhttp:3.14.0"
    implementation "com.squareup.okio:okio:2.2.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.30"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from("src/main/resources") {
        include "plugin.properties"
        expand(
                version: "${project.parent.version}",
                name: project.parent.name,
                commit: "${git.head().abbreviatedId}",
                date: "${git.head().getDate().format("yy.MM.dd")}",
        )
    }
}

jar.setArchiveName("PlotSquared-API-${project.parent.version}.jar")
jar.destinationDir = file "../mvn/com/github/intellectualsites/plotsquared/PlotSquared-API/" + project.parent.version
task createPom {
    doLast {
        pom {
            project {
                groupId "com.github.intellectualsites.plotsquared"
                artifactId "PlotSquared-API"
                version project.parent.version
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-API/${project.parent.version}/PlotSquared-API-${project.parent.version}.pom")
        pom {
            project {
                groupId "com.github.intellectualsites.plotsquared"
                artifactId "PlotSquared-API"
                version "latest"
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/PlotSquared-API/latest/PlotSquared-API-latest.pom")
    }
}

task copyFiles {
    doLast {
        copy {
            from "../mvn/com/github/intellectualsites/plotsquared/PlotSquared-API/${project.parent.version}/"
            into "../mvn/com/github/intellectualsites/plotsquared/PlotSquared-API/latest/"
            include("*.jar")
            rename("PlotSquared-API-${project.parent.version}.jar", "PlotSquared-API-latest.jar")
        }
    }
}

build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
